# Split something you've done into two parts. This is useful if you've forgotten to stop a did at some point. To use it you simply need to find the specific did and then call splitDid like this --splitDid=secondsAfterStartToSplit[,taskName[,who[,comment]]] ~ doneit,user

# TODO add unit tests for splitDid
# TODO refactor in parameters
# TODO Currently this feature does some funky things when working with more than one record. Once this is fixed, first can be removed.
# first

parameters secondsAfterStartToSplit,taskName,who,comment

thereCanBeOnly 1,splitDid,
	loop
		# Check that we have sane values
		set Tmp,passed,true

		if ~!Local,secondsAfterStartToSplit!~,<,0,
			set Tmp,passed,false
			debug 0,splitDid: secondsAfterStartToSplit (~!Local,secondsAfterStartToSplit!~) is too small

		basicMaths Tmp,splitTime,~!Result,start!~,+,~!Local,secondsAfterStartToSplit!~


		if ~!Result,stop!~,!=,,
			if ~!Tmp,splitTime!~,>,~!Result,stop!~,
				set Tmp,passed,false
				debug 0,splitDid: secondsAfterStartToSplit is greater than the duration of the did.

		if ~!Tmp,passed!~,==,true,
			addDebuggingToDid ~!Result,start!~,splitDid: Creating new did ~!Tmp,splitTime!~

			debug 1,closeDid ~!Result,start!~,~!Tmp,splitTime!~
			closeDid ~!Result,start!~,~!Tmp,splitTime!~

			# TODO write code to take optional information
			if ~!Local,taskName!~,!=,,
				set Tmp,taskName,~!Local,taskName!~
			else
				set Tmp,taskName,~!Result,taskName!~

			if ~!Local,who!~,!=,,
				set Tmp,who,~!Local,who!~
			else
				set Tmp,who,~!Result,who!~

			if ~!Local,comment!~,!=,,
				set Tmp,comment,~!Local,comment!~
			else
				set Tmp,comment,~!Result,comment!~

			addDebuggingToDid ~!Tmp,splitTime!~,splitDid: Created from ~!Result,start!~
			debug 1,openDid ~!Tmp,taskName!~,~!Tmp,who!~,~!Tmp,comment!~,~!Tmp,splitTime!~
			openDid ~!Tmp,taskName!~,~!Tmp,who!~,~!Tmp,comment!~,~!Tmp,splitTime!~

			if ~!Result,stop!~,!=,,
				# We have a stop position
				debug 1,closeDid ~!Tmp,splitTime!~,~!Result,stop!~
				closeDid ~!Tmp,splitTime!~,~!Result,stop!~


		# TODO Migrate to Local variables.
		unset Tmp,passed
		unset Tmp,splitTime
		unset Tmp,taskName
		unset Tmp,who
		unset Tmp,comment

	stashResults Tmp,dids
	sortDids
	retrieveResults Tmp,dids

	# TODO Migrate to Local variables.
	unset Tmp,dids
