# Split something you've done into two parts. This is useful if you've forgotten to stop a did at some point. To use it you simply need to find the specific did and then call splitDid like this --splitDid=secondsAfterStartToSplit[,taskName[,who[,comment]]] ~ doneit,user

# TODO add unit tests for splitDid
# TODO refactor in parameters
# TODO Currently this feature does some funky things when working with more than one record. Once this is fixed, first can be removed.
# first

parameters secondsAfterStartToSplit,taskName,who,comment
set Me,secondsAfterStartToSplit,~!Local,secondsAfterStartToSplit!~
set Me,taskName,~!Local,taskName!~
set Me,who,~!Local,who!~
set Me,comment,~!Local,comment!~

thereCanBeOnly 1,splitDid,
	loop
		# Check that we have sane values
		set Tmp,passed,true

		if ~!Result,start!~,==,,
			set Tmp,passed,false
			debug 0,splitDid: start (~!Result,start!~) of the selected "did" does not appear to be set. This is usually a corruption. It is not safe to run splitDid on this entry.

		if ~!Result,stop!~,==,,
			set Tmp,passed,false
			debug 0,splitDid: stop (~!Result,stop!~) of the selected "did" does not appear to be set. This is usually a corruption. It is not safe to run splitDid on this entry.

		if ~!Me,secondsAfterStartToSplit!~,==,0,
			set Tmp,passed,false
			debug 0,splitDid: secondsAfterStartToSplit (~!Me,secondsAfterStartToSplit!~) is too small

		if ~!Me,secondsAfterStartToSplit!~,>,0,
			basicMaths Tmp,splitTime,~!Result,start!~,+,~!Me,secondsAfterStartToSplit!~
			debug 1,splitDid: Forward: basicMaths ~!Tmp,splitTime!~,~!Result,start!~,+,~!Me,secondsAfterStartToSplit!~
		else
			# Negative offset. It should be offset from the stop instead of the start.
			basicMaths Tmp,splitTime,~!Result,stop!~,+,~!Me,secondsAfterStartToSplit!~
			debug 1,splitDid: Reverse: basicMaths ~!Tmp,splitTime!~,~!Result,stop!~,+,~!Me,secondsAfterStartToSplit!~

		basicMaths Local,previousDuration,~!Result,stop!~,-,~!Result,start!~
		basicMaths Local,previousDuration,~!Local,previousDuration!~,-,1


		if ~!Tmp,splitTime!~,==,~!Result,start!~,
			set Tmp,passed,false
			debug 0,splitDid: The start time for the new "did" was not successfully calculated. This is usually a invalid input from the user. It is not safe to run splitDid on this entry.
			isolate
				help splitDid
				finishHelp
				outNow

		if ~!Tmp,splitTime!~,<=,~!Result,start!~,
			set Tmp,passed,false
			debug 0,splitDid: Calculated splitTime (~!Tmp,splitTime!~) is not greater than  the start (~!Result,start!~) of the previous did. You've likely entered a negative offset (~!Me,secondsAfterStartToSplit!~) that is too small. Minimum possible value for this did: -~!Local,previousDuration!~ .

		if ~!Tmp,splitTime!~,>=,~!Result,stop!~,
			set Tmp,passed,false
			debug 0,splitDid: Calculated splitTime (~!Tmp,splitTime!~) is not less than  the stop (~!Result,stop!~) of the previous did. You've likely entered a negative offset (~!Me,secondsAfterStartToSplit!~) that is too big. Maximum value for this did: ~!Local,previousDuration!~ .

		if ~!Result,stop!~,!=,,
			if ~!Tmp,splitTime!~,>,~!Result,stop!~,
				set Tmp,passed,false
				debug 0,splitDid: secondsAfterStartToSplit is greater than the duration of the did.

		if ~!Tmp,passed!~,==,true,
			addDebuggingToDid ~!Result,start!~,splitDid: Creating new did ~!Tmp,splitTime!~

			debug 1,closeDid ~!Result,start!~,~!Tmp,splitTime!~
			closeDid ~!Result,start!~,~!Tmp,splitTime!~

			# TODO write code to take optional information
			if ~!Me,taskName!~,!=,,
				set Tmp,taskName,~!Me,taskName!~
			else
				set Tmp,taskName,~!Result,taskName!~

			if ~!Me,who!~,!=,,
				set Tmp,who,~!Me,who!~
			else
				set Tmp,who,~!Result,who!~

			if ~!Me,comment!~,!=,,
				set Tmp,comment,~!Me,comment!~
			else
				set Tmp,comment,~!Result,comment!~

			addDebuggingToDid ~!Tmp,splitTime!~,splitDid: Created from ~!Result,start!~
			debug 1,openDid ~!Tmp,taskName!~,~!Tmp,who!~,~!Tmp,comment!~,~!Tmp,splitTime!~
			openDid ~!Tmp,taskName!~,~!Tmp,who!~,~!Tmp,comment!~,~!Tmp,splitTime!~

			if ~!Result,stop!~,!=,,
				# We have a stop position
				debug 1,closeDid ~!Tmp,splitTime!~,~!Result,stop!~
				closeDid ~!Tmp,splitTime!~,~!Result,stop!~


		# TODO Migrate to Me variables.
		unset Tmp,passed
		unset Tmp,splitTime
		unset Tmp,taskName
		unset Tmp,who
		unset Tmp,comment

	stashResults Tmp,dids
	sortDids
	retrieveResults Tmp,dids

	# TODO Migrate to Me variables.
	unset Tmp,dids
