# What are you doing right now? And for who? --doing=name[,who[,description[,overrideTimestamp]]] eg --doing='fixComp',bob,'fixing computer' --doing=fixComp ~ doneit

# Do we match any tasks
listTasks ~!Global,doing-0!~
countToVar Tmp,count


set Tmp,createNew,false
if ~!Tmp,count!~,==,0,
	# We don't have a task to do.
	
	if ~!Global,doing-2!~,==,,
		debug 0,doing: I can't find a task matching "^~!Global,doing-0!~$". If you'd like to add it please include who and a description.
	else
		if ~!Global,doing-1!~,==,,
			debug 0,doing: I can't find a task matching "^~!Global,doing-0!~$". If you'd like to add it please include who and a description.
		else
			debug 1,doing: Adding task ~!Global,doing-2!~
			addTask ~!Global,doing-0!~,~!Global,doing-1!~,~!Global,doing-2!~
			listTasks ~!Global,doing-0!~
			countToVar Tmp,count
			set Tmp,createNew,true

if ~!Tmp,count!~,!=,0,
	# We have a task to do, let's do it.
	
	
	# figure out when the change happens
	if ~!Global,doing-3!~,==,,
		debug 1,doing: No time specified, setting it to now
		now Tmp,changeOver
	elseIf ~!Global,doing-3!~,>,~!Time,fuzzyTimeThreshold!~,
		debug 1,doing: It's either a really big offset, or it's an absolute time. Assume it's an absolute time.
		set Tmp,changeOver,~!Global,doing-3!~
	else
		if ~!Global,doing-3!~,<,0,
			debug 1,doing: It's a negative offset, go from now+(-offset)
			now Tmp,now
			basicMaths Tmp,changeOver,~!Tmp,now!~,+,~!Global,doing-3!~
			unset Tmp,now
		else
			debug 1,doing: It's a positive offset, go from the end of the previous task + offset
			basicMaths Tmp,changeOver,~!DoneIt,dids,~!DoneIt,lastDid!~,start!~,+,~!Global,doing-3!~
	
	
	# Get task ID of the task to be done
	first
	loop
		set DoneIt,lastTask,~!Result,name!~
		set DoneIt,lastWho,~!Result,lastWho!~
	
	# Close off the old task
	if ~!DoneIt,lastDid!~,!=,,
		closeDid ~!DoneIt,lastDid!~,~!Tmp,changeOver!~
	else
		debug 1,doing: No previous did to close.
	
	# Save the task
	debug 1,doing: Setting new task to start from ~!Tmp,changeOver!~
	set DoneIt,lastDid,~!Tmp,changeOver!~
	
	if ~!Global,doing-1!~,!=,,
		debug 2,doing: Setting who to ~!Global,doing-1!~
		set DoneIt,lastWho,~!Global,doing-1!~
	
	# TODO id->ID
	
	# TODO finish this
	if ~!Tmp,createNew!~,==,false,
		openDid ~!DoneIt,lastTask!~,~!DoneIt,lastWho!~,~!Global,doing-2!~,~!DoneIt,lastDid!~
	else
		openDid ~!DoneIt,lastTask!~,~!DoneIt,lastWho!~,,~!DoneIt,lastDid!~


unset Tmp,count
unset Tmp,createNew
unset Tmp,changeOver
clear
nested
