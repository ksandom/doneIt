# What are you doing right now? And for who? --doing=name[,who[,description[,overrideTimestamp]]] eg --doing='fixComp',bob,'fixing computer' --doing=fixComp ~ doneit

# Do we match any tasks
listTasks ~!Global,doing-0!~
countToVar Tmp,count

if ~!Tmp,count!~,==,0,
	# We don't have a task to do.
	
	if ~!Global,doing-2!~,==,,
		debug 0,doing: I can't find a task matching "^~!Global,doing-0!~$". If you'd like to add it please include who and a description.
	else
		if ~!Global,doing-1!~,==,,
			debug 0,doing: I can't find a task matching "^~!Global,doing-0!~$". If you'd like to add it please include who and a description.
		else
			debug 0,doing: Adding task ~!Global,doing-2!~
			addTask ~!Global,doing-0!~,~!Global,doing-1!~,~!Global,doing-2!~
			listTasks ~!Global,doing-0!~
			countToVar Tmp,count

if ~!Tmp,count!~,!=,0,
	# We have a task to do, let's do it.
	
	# Get task ID of the task to be done
	first
	loop
		set DoneIt,lastTask,~!Result,name!~
		set DoneIt,lastWho,~!Result,lastWho!~
	
	# Close off the old task
	if ~!DoneIt,lastDid!~,!=,,
		# TODO fix this
		
		if ~!Global,doing-3!~,==,,
			now Tmp,stop
		else
			debug 0,doing: Will close off the old task at ~!Global,doing-3!~
			set Tmp,stop,~!Global,doing-3!~
		
		setNested DoneIt,dids,~!DoneIt,lastDid!~,stop,~!Tmp,stop!~
		timeDiff Tmp,duration,~!DoneIt,dids,~!DoneIt,lastDid!~,start!~,~!DoneIt,dids,~!DoneIt,lastDid!~,stop!~
		setNested DoneIt,dids,~!DoneIt,lastDid!~,duration,~!Tmp,duration!~
		
		fuzzyTime Tmp,fuzzyDuration,~!DoneIt,dids,~!DoneIt,lastDid!~,duration!~
		setNested DoneIt,dids,~!DoneIt,lastDid!~,fuzzyDuration,~!Tmp,fuzzyDuration!~
		
		debug 0,doing: Closed did ~!DoneIt,lastDid!~ (~!DoneIt,tasks,~!DoneIt,dids,~!DoneIt,lastDid!~,taskName!~,description!~) lasting ~!Tmp,duration!~ seconds (~!Tmp,fuzzyDuration!~)
		
		unset Tmp,duration
		unset Tmp,fuzzyDuration
		unset Tmp,stop
	else
		debug 0,doing: No previous did to close.
	
	# Save the task
	if ~!Global,doing-3!~,==,,
		now DoneIt,lastDid
	else
		debug 0,doing: Setting new task to start from ~!Global,doing-3!~
		set DoneIt,lastDid,~!Global,doing-3!~
	
	# TODO id->ID
	debug 0,doing: Starting ~!DoneIt,tasks,~!DoneIt,lastTask!~,description!~
	setNested DoneIt,dids,~!DoneIt,lastDid!~,id,~!DoneIt,lastDid!~
	setNested DoneIt,dids,~!DoneIt,lastDid!~,taskName,~!DoneIt,lastTask!~
	setNested DoneIt,dids,~!DoneIt,lastDid!~,start,~!DoneIt,lastDid!~
	setNested DoneIt,dids,~!DoneIt,lastDid!~,who,~!DoneIt,lastWho!~

unset Tmp,count
clear
nested
