# What are you doing right now? And for who? --doing=name[,who[,description]] eg --doing='fixComp',bob,'fixing computer' --doing=fixComp ~ doneit

# Do we match any tasks
listTasks ~!Global,doing-0!~
countToVar Tmp,count

if ~!Tmp,count!~,==,0,
	# We don't have a task to do.
	
	if ~!Global,addTask-2!~,==,,
		debug 0,DoneIt: I can't find a task matching "^~!Global,addTask-0!~$". If you'd like to add it please include who and a description.
	else
		if ~!Global,addTask-1!~,==,,
			debug 0,DoneIt: I can't find a task matching "^~!Global,addTask-0!~$". If you'd like to add it please include who and a description.
		else
			addTask ~!Global,addTask-0!~,~!Global,addTask-1!~,~!Global,addTask-2!~
else
	# We have a task to do, let's do it.
	
	# Get task ID of the task to be done
	first
	loop
		set DoneIt,lastTask,~!Result,name!~
		set DoneIt,lastWho,~!Result,lastWho!~
	
	# Close off the old task
	if ~!DoneIt,lastDid!~,!=,,
		# TODO fix this
		now Tmp,stop
		setNested DoneIt,dids,~!DoneIt,lastDid!~,stop,~!Tmp,stop!~
		timeDiff Tmp,duration,~!DoneIt,dids,~!DoneIt,lastDid!~,start!~,~!DoneIt,dids,~!DoneIt,lastDid!~,stop!~
		setNested DoneIt,dids,~!DoneIt,lastDid!~,duration,~!Tmp,duration!~
		
		fuzzyTime Tmp,fuzzyDuration,~!DoneIt,dids,~!DoneIt,lastDid!~,duration!~
		setNested DoneIt,dids,~!DoneIt,lastDid!~,fuzzyDuration,~!Tmp,fuzzyDuration!~
		
		debug 0,DoneIt: Closed did ~!DoneIt,lastDid!~ (~!DoneIt,tasks,~!DoneIt,dids,~!DoneIt,lastDid!~,taskName!~,description!~) lasting ~!Tmp,duration!~ seconds (~!Tmp,fuzzyDuration!~)
		
		unset Tmp,duration
		unset Tmp,fuzzyDuration
		unset Tmp,stop
	else
		debug 0,DoneIt: No previous did to close.
	
	# Save the task
	now DoneIt,lastDid
	# TODO id->ID
	setNested DoneIt,dids,~!DoneIt,lastDid!~,id,~!DoneIt,lastDid!~
	setNested DoneIt,dids,~!DoneIt,lastDid!~,taskName,~!DoneIt,lastTask!~
	setNested DoneIt,dids,~!DoneIt,lastDid!~,start,~!DoneIt,lastDid!~
	setNested DoneIt,dids,~!DoneIt,lastDid!~,who,~!DoneIt,lastWho!~

unset Tmp,count
clear
nested
